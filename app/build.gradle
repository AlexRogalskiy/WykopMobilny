apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

kapt {
    generateStubs = true
    correctErrorTypes = true
}

androidExtensions {
    experimental = true
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease')
        task.dependsOn('test')
}

static def getBugsnagKey() {
    return System.getenv('BUGSNAG_API_KEY') ? System.getenv('BUGSNAG_API_KEY') : ''
}

static def isWeeklyBuild() {
    return System.getenv('TRAVIS_EVENT_TYPE') == "cron" ? "true" : "false"
}

android {
    compileSdkVersion 27
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "io.github.feelfreelinux.wykopmobilny"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode project.ext.versionMajor * 10000 + project.ext.versionMinor * 1000 + project.ext.versionPatch * 100 + project.ext.versionBuild
        versionName "${project.ext.versionMajor}.${project.ext.versionMinor}.${project.ext.versionPatch}.${project.ext.versionBuild}"
        multiDexEnabled true

        def credentialsPropertiesFile = rootProject.file("credentials.properties")
        def credentialsProperties = new Properties()
        credentialsProperties.load(new FileInputStream(credentialsPropertiesFile))

        buildConfigField "String", "IS_WEEKLY", "\"${isWeeklyBuild()}\""
        buildConfigField "String", "BUGSNAG_KEY", "\"${getBugsnagKey()}\""
        buildConfigField 'String', 'APP_SECRET', credentialsProperties['apiSecret']
        buildConfigField 'String', 'APP_KEY', credentialsProperties['apiKey']
    }
    
    signingConfigs {
        ci {
            keyAlias "key0"
            keyPassword System.getenv('CI_KEYSTORE_PASSWORD')
            storeFile file('../ci-release-keystore.jks')
            storePassword System.getenv('CI_KEYSTORE_PASSWORD')
        }
    }
    
    buildTypes {
        release {
            if(System.getenv('CI_KEYSTORE_PASSWORD')) signingConfig signingConfigs.ci
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        jumboMode true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    mavenCentral()
	maven { url 'https://jitpack.io' }
    maven { url 'http://dl.bintray.com/jetbrains/markdown' }
    maven { url  "http://dl.bintray.com/piasy/maven" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"

    // Support library
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.android.support:recyclerview-v7:$versions.supportLibrary"
    implementation "com.android.support:exifinterface:$versions.supportLibrary"
    implementation "com.android.support:cardview-v7:$versions.supportLibrary"
    implementation "com.android.support:preference-v7:$versions.supportLibrary"
    implementation "com.android.support:preference-v14:$versions.supportLibrary"
    implementation "com.android.support:design:$versions.supportLibrary"
    implementation "com.android.support:support-v4:$versions.supportLibrary"
    implementation "com.android.support:customtabs:$versions.supportLibrary"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'
    implementation "com.google.android.gms:play-services-gcm:11.8.0"
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.devbrackets.android:exomedia:4.1.0'
    implementation 'com.google.android.exoplayer:extension-okhttp:2.6.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'com.google.code.gson:gson:2.8.2'

    // Utils
    implementation 'com.github.PierfrancescoSoffritti:AndroidYouTubePlayer:4.1.5'
    implementation "com.bugsnag:bugsnag-android:4.1.3"
    implementation "org.ocpsoft.prettytime:prettytime:$versions.prettytime"
    implementation "com.jsibbold:zoomage:1.1.0"
    implementation "com.github.chrisbanes:PhotoView:2.1.3"
    implementation "com.pavelsikun:material-seekbar-preference:2.3.0"
    implementation 'com.github.feelfreelinux:Slidr:v2.2'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-jackson:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp_interceptor"
    implementation 'pl.droidsonroids:jspoon:1.3.0'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.0'

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation  'org.jetbrains:markdown:0.1.25'

    // AppUpdater
    implementation "com.github.javiersantos:AppUpdater:$versions.appUpdater"


    // Dagger 2
    implementation "com.google.dagger:dagger:2.13"
    kapt "com.google.dagger:dagger-compiler:2.13"
    kapt "com.google.dagger:dagger-android-processor:2.13"
    implementation "com.google.dagger:dagger-android:2.13"
    implementation "com.google.dagger:dagger-android-support:2.13"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Glide
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glide"
    implementation 'com.google.code.gson:gson:2.8.2'

    // Android-Job
    implementation "com.evernote:android-job:$versions.androidjob"

    // Unit tests
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "com.nhaarman:mockito-kotlin:$versions.mockito_kotlin"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
